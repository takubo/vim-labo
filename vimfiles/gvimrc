vim9script
# vim: set ts=8 sts=2 sw=2 tw=0 et:
scriptencoding utf-8



#---------------------------------------------------------------------------------------------
# Plugin

if has('win32')
  packadd vimtweak
endif



#---------------------------------------------------------------------------------------------
# Font


#--------------------------------------------
# Resize Font

def ResizeFont(size: float)
  const gf = &guifont
  const hx = matchstr(gf, '\<h\zs\d\+\%(.\d*\)\>')
  const hn = str2float(hx) + size
  &guifont = substitute(gf, '\<h\zs\d\+\%(.\d*\)\>', hn, '')
enddef

def GetFontSize(): float
  const gf = &guifont
  const hx = matchstr(gf, '\<h\zs\d\+\%(.\d*\)\>')
  return str2float(hx)
enddef

com! -bar -nargs=? -bang ResizeFont {
                                      const a = <bang>true ? str2float(<q-args>)
                                                           : ((<q-args> == '' ? DefaultFontSize : str2float(<q-args>)) - GetFontSize())
                                      call <SID>ResizeFont(a)
                                      redraw
                                      echo <SID>GetFontSize()
                                    }

nnoremap <A-Up>   <Cmd>ResizeFont +0.5<CR>
nnoremap <A-Down> <Cmd>ResizeFont -0.5<CR>



#---------------------------------------------------------------------------------------------
# Window Position

nnoremap <silent> <Left>    <Cmd>exe 'winpos'    getwinposx() - 8 * v:count1    getwinposy()               <CR>
nnoremap <silent> <Down>    <Cmd>exe 'winpos'    getwinposx()                   getwinposy() + 8 * v:count1<CR>
nnoremap <silent> <Up>      <Cmd>exe 'winpos'    getwinposx()                   getwinposy() - 8 * v:count1<CR>
nnoremap <silent> <Right>   <Cmd>exe 'winpos'    getwinposx() + 8 * v:count1    getwinposy()               <CR>

nnoremap <silent> <S-Left>  <Cmd>exe 'winpos'    getwinposx() - 1 * v:count1    getwinposy()               <CR>
nnoremap <silent> <S-Down>  <Cmd>exe 'winpos'    getwinposx()                   getwinposy() + 1 * v:count1<CR>
nnoremap <silent> <S-Up>    <Cmd>exe 'winpos'    getwinposx()                   getwinposy() - 1 * v:count1<CR>
nnoremap <silent> <S-Right> <Cmd>exe 'winpos'    getwinposx() + 1 * v:count1    getwinposy()               <CR>



#---------------------------------------------------------------------------------------------
# Window Size


#--------------------------------------------
# Window Size Change

nnoremap <silent> <C-Left>    <Cmd>exe 'let &columns -=' 3 * v:count1<CR>
nnoremap <silent> <C-Down>    <Cmd>exe 'let &lines   +=' 2 * v:count1<CR>
nnoremap <silent> <C-Up>      <Cmd>exe 'let &lines   -=' 2 * v:count1<CR>
nnoremap <silent> <C-Right>   <Cmd>exe 'let &columns +=' 3 * v:count1<CR>

nnoremap <silent> <C-S-Left>  <Cmd>exe 'let &columns -=' 1 * v:count1<CR>
nnoremap <silent> <C-S-Down>  <Cmd>exe 'let &lines   +=' 1 * v:count1<CR>
nnoremap <silent> <C-S-Up>    <Cmd>exe 'let &lines   -=' 1 * v:count1<CR>
nnoremap <silent> <C-S-Right> <Cmd>exe 'let &columns +=' 1 * v:count1<CR>


#--------------------------------------------
# FullScreen

if has('win32')
  com! -bang -bar FullScreen {
                               const lines   = &lines
                               const columns = &columns

                               if '<bang>' == '!'
                                 # 先にCacptionを消してから全画面表示にすると、タスクバーの上にもウィンドウが広がる。
                                 # VimTweakDisableCaption
                                 libcallnr(g:vimtweak_dll_path, "EnableCaption", 0)
                               else
                                 # VimTweakEnableCaption
                                 libcallnr(g:vimtweak_dll_path, "EnableCaption", 1)
                               endif
                               # VimTweakEnableMaximize
                               libcallnr(g:vimtweak_dll_path, "EnableMaximize", 1)
                               # VimTweakDisableCaption
                               libcallnr(g:vimtweak_dll_path, "EnableCaption", 0)

                               if lines == &lines && columns == &columns
                                 if '<bang>' != '!'
                                   # VimTweakEnableCaption
                                   libcallnr(g:vimtweak_dll_path, "EnableCaption", 1)
                                 endif
                                 # VimTweakDisableMaximize
                                 libcallnr(g:vimtweak_dll_path, "EnableMaximize", 0)
                               endif
                             }
endif


nnoremap <A-CR> <Cmd>FullScreen!<CR>



#---------------------------------------------------------------------------------------------
# Decoration


if has('win32')
  com! -bar TitleEnable  VimTweakEnableCaption
  com! -bar TitleDisable VimTweakDisableCaption
endif



#---------------------------------------------------------------------------------------------
# Transparency


if has('win32')
  #var Alpha = 255
  g:Alpha = 255

  def SetTransparency(add: float)
    const cur = g:Alpha * 100.0 / 255.0
    const new = cur + add
    echo cur add new
    exe 'SetTransparency' new
  enddef

  com! -nargs=? -bar -bang SetTransparency {
                                              var alpha: number
                                              if '<bang>' == '!'
                                                alpha = 255
                                              else
                                                const tmp: float = str2nr(<q-args>) / 100.0 * 255
                                                alpha = float2nr(tmp)
                                                alpha = max([  0, alpha])
                                                alpha = min([255, alpha])
                                              endif
                                              call libcallnr(g:vimtweak_dll_path, "SetAlpha", alpha)
                                              g:Alpha = alpha
                                            }
endif


#nnoremap <A-Left>  <ScriptCmd>SetTransparency(-0.5)<CR>
#nnoremap <A-Right> <ScriptCmd>SetTransparency(+0.5)<CR>
nnoremap <A-Left>  <ScriptCmd>SetTransparency(-1.5 * v:count1)<CR>
nnoremap <A-Right> <ScriptCmd>SetTransparency(+1.5 * v:count1)<CR>



#---------------------------------------------------------------------------------------------
# Options

set guioptions=!cd
# set visualbell t_vb=



#---------------------------------------------------------------------------------------------
# GUI Cursor
# TODO


#set guicursor=n-v-c:block-Cursor/lCursor,ve:ver35-Cursor,o:hor50-Cursor,i-ci:ver25-Cursor/lCursor,r-cr:hor20-Cursor/lCursor,a:blinkon0
#set guicursor=n-v-c:ver10-Cursor/lCursor,ve:ver35-Cursor,o:hor50-Cursor,i-ci:ver25-Cursor/lCursor,r-cr:hor20-Cursor/lCursor,a:blinkon0

set guicursor=n-v-c:ver20-blinkwait1000-blinkon600-blinkoff400-Cursor/lCursor,ve:ver35-Cursor,o:hor50-Cursor,i-ci:hor30-Cursor/lCursor,r-cr:Block-Cursor/lCursor



#---------------------------------------------------------------------------------------------
# Initialize


#--------------------------------------------
# Font

if has('win32')
  set guifont=BIZ_UDゴシック:h11:cSHIFTJIS:qDRAFT
endif

const DefaultFontSize = GetFontSize()


#--------------------------------------------
# Window Position

winpos 600 800


#--------------------------------------------
# Window Size

set lines=75 columns=240


if has('win32')
  augroup MyGvimrc
    au!


    #--------------------------------------------
    # Decoration

    au VimEnter * TitleDisable

    #--------------------------------------------

    # Transparency

    #au VimEnter * VimTweakSetAlpha 220
    #au VimEnter * SetTransparency 92  # 235
    au VimEnter * SetTransparency 87


  augroup end
endif
